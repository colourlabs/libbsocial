cmake_minimum_required(VERSION 3.12)
project(libbsocial C)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")

include(CheckCSourceCompiles)
include(CMakePushCheckState)
include(GNUInstallDirs)
include(GenerateExportHeader)

option(BUILD_SHARED_LIBS "Build as a shared library" TRUE)
option(EXAMPLES "Build examples" TRUE)

find_package(PkgConfig REQUIRED)
set(LIBBSOCIAL_PKGCONFIG_DEPS jansson libcurl)
pkg_check_modules(PC_LIBBSOCIAL_DEPS REQUIRED IMPORTED_TARGET ${LIBBSOCIAL_PKGCONFIG_DEPS})

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/include/bsocial.h" version)
string(REGEX MATCH "BSOCIAL_VERSION_MAJOR ([0-9]*)" _ ${version})
set(LIBBSOCIAL_VERSION_MAJOR ${CMAKE_MATCH_1})
string(REGEX MATCH "BSOCIAL_VERSION_MINOR ([0-9]*)" _ ${version})
set(LIBBSOCIAL_VERSION_MINOR ${CMAKE_MATCH_1})

set(CMAKE_CXX_VISIBILITY_PRESET hidden)
set(CMAKE_C_STANDARD 90)
file(GLOB LIBBSOCIAL_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/*.c")
add_library(libbsocial ${LIBBSOCIAL_SRC})
generate_export_header(libbsocial BASE_NAME "bsocial" EXPORT_MACRO_NAME "BSOCIAL_EXPORT" EXPORT_FILE_NAME "bsocial-export.h")
add_library(libbsocial::libbsocial ALIAS libbsocial)
set_property(TARGET libbsocial PROPERTY OUTPUT_NAME "bsocial")
set_property(TARGET libbsocial PROPERTY C_VISIBILITY_PRESET ${CMAKE_CXX_VISIBILITY_PRESET})
set_property(TARGET libbsocial PROPERTY POSITION_INDEPENDENT_CODE ON)
target_include_directories(libbsocial PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>")
target_include_directories(libbsocial PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>")
target_include_directories(libbsocial PUBLIC "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>")
target_link_libraries(libbsocial PRIVATE PkgConfig::PC_LIBBSOCIAL_DEPS)

string(JOIN " " LIBBSOCIAL_REQUIRES_PRIVATE ${LIBBSOCIAL_PKGCONFIG_DEPS})
configure_file(cmake/libbsocial.pc.cmake libbsocial.pc @ONLY)

file(GLOB LIBBSOCIAL_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/include/*.h")
foreach(LIBBSOCIAL_HEADER ${LIBBSOCIAL_HEADERS})
	install(FILES ${LIBBSOCIAL_HEADER} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")
endforeach()
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/bsocial-export.h" DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")

install(FILES "${CMAKE_CURRENT_BINARY_DIR}/libbsocial.pc" DESTINATION "${CMAKE_INSTALL_LIBDIR}/pkgconfig")
install(TARGETS libbsocial ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}" LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}" RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}")

if(EXAMPLES)
	file(GLOB LIBBSOCIAL_EXAMPLES "${CMAKE_CURRENT_SOURCE_DIR}/examples/*.c")
	foreach(LIBBSOCIAL_EXAMPLE ${LIBBSOCIAL_EXAMPLES})
		get_filename_component(LIBBSOCIAL_EXAMPLE_CMAKE_1  ${LIBBSOCIAL_EXAMPLE} NAME)
		string(REGEX REPLACE "\\.[^.]*$" "" LIBBSOCIAL_EXAMPLE_CMAKE ${LIBBSOCIAL_EXAMPLE_CMAKE_1})
		add_executable(${LIBBSOCIAL_EXAMPLE_CMAKE} ${LIBBSOCIAL_EXAMPLE})
		target_link_libraries(${LIBBSOCIAL_EXAMPLE_CMAKE} PRIVATE libbsocial::libbsocial)
	endforeach()	
endif()
