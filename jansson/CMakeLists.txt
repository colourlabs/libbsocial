file(READ "${CMAKE_CURRENT_SOURCE_DIR}/include/bsocial-jansson.h" version)
string(REGEX MATCH "BSOCIAL_JANSSON_VERSION_MAJOR ([0-9]*)" _ ${version})
set(LIBBSOCIAL_JANSSON_VERSION_MAJOR ${CMAKE_MATCH_1})
string(REGEX MATCH "BSOCIAL_JANSSON_VERSION_MINOR ([0-9]*)" _ ${version})
set(LIBBSOCIAL_JANSSON_VERSION_MINOR ${CMAKE_MATCH_1})

find_package(PkgConfig REQUIRED)
pkg_check_modules(JANSSONPC REQUIRED jansson)
file(GLOB LIBBSOCIAL_JANSSON_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/*.c")
add_library(libbsocialjansson ${LIBBSOCIAL_JANSSON_SRC})
add_dependencies(libbsocialjansson libbsocialcore)
set_property(TARGET libbsocialjansson PROPERTY OUTPUT_NAME "bsocial-jansson")
set_property(TARGET libbsocialjansson PROPERTY C_VISIBILITY_PRESET hidden)
set_property(TARGET libbsocialjansson PROPERTY POSITION_INDEPENDENT_CODE ON)
get_target_property(LIBBSOCIAL_CORE_INCLUDES libbsocialcore INCLUDE_DIRECTORIES)
target_include_directories(libbsocialjansson PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>")
target_include_directories(libbsocialjansson PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>")
target_include_directories(libbsocialjansson PUBLIC "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>")
target_include_directories(libbsocialjansson PRIVATE ${JANSSONPC_INCLUDE_DIRS})
target_include_directories(libbsocialjansson PRIVATE ${LIBBSOCIAL_CORE_INCLUDES})
target_link_libraries(libbsocialjansson PRIVATE ${JANSSONPC_LIBRARIES})
target_compile_options(libbsocialjansson PRIVATE ${JANSSONPC_CFLAGS_OTHER})

string(JOIN " " LIBBSOCIAL_JANSSON_REQUIRES_PRIVATE "jansson")
configure_file("${CMAKE_CURRENT_LIST_DIR}/cmake/libbsocial-jansson.pc.cmake" libbsocial-jansson.pc @ONLY)

file(GLOB LIBBSOCIAL_JANSSON_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/include/*.h")
foreach(LIBBSOCIAL_JANSSON_HEADER ${LIBBSOCIAL_JANSSON_HEADERS})
	if(NOT (${LIBBSOCIAL_JANSSON_HEADER} MATCHES "priv.h$"))
		install(FILES ${LIBBSOCIAL_JANSSON_HEADER} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")
	endif()
endforeach()

install(FILES "${CMAKE_CURRENT_BINARY_DIR}/libbsocial-jansson.pc" DESTINATION "${CMAKE_INSTALL_LIBDIR}/pkgconfig")
install(TARGETS libbsocialjansson ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}" LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}" RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}")

